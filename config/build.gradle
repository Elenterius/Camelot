import net.neoforged.gradleutils.PomUtilsExtension

plugins {
    id 'groovy'
    id 'java-library'
    id 'maven-publish'
}

group = rootProject.group
version = rootProject.version

rootProject.gradleutils {
    setupSigning(project: this.project, signAllPublications: true)
}

repositories {
    mavenCentral()
}

java.withSourcesJar()

groovydoc {
    use = true
}

tasks.register('groovydocJar', Jar) {
    dependsOn tasks.groovydoc
    archiveClassifier = 'javadoc'
    from groovydoc.destinationDir
}

dependencies {
    api "org.apache.groovy:groovy:${project.groovy_version}"
    api "org.apache.groovy:groovy-contracts:${project.groovy_version}"
    implementation group: 'org.kohsuke', name: 'github-api', version: project.ghapi_version
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact(tasks.groovydocJar)
            artifactId = 'camelot-config'
            pom {
                name = 'Camelot configuration'
                description = 'Configuration for the Camelot Discord bot'
                rootProject.pomUtils.githubRepo(it, 'Camelot')
                rootProject.pomUtils.neoForgedDeveloper(it)
                rootProject.pomUtils.license(it, PomUtilsExtension.License.MIT)
            }
        }
    }
}

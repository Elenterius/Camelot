import net.neoforged.gradleutils.PomUtilsExtension

plugins {
    id 'groovy'
    id 'java-library'
    id 'maven-publish'
}

group = rootProject.group
version = rootProject.version

rootProject.gradleutils {
    setupSigning(project: this.project, signAllPublications: true)
}

sourceSets {
    sample {
        compileClasspath += main.compileClasspath
    }
}

repositories {
    mavenCentral()
}

configurations {
    configSources
}

java.withSourcesJar()

groovydoc {
    use = true
    source(configurations.configSources)
}

tasks.register('groovydocJar', Jar) {
    dependsOn tasks.groovydoc
    archiveClassifier = 'javadoc'
    from groovydoc.destinationDir
}

dependencies {
    api "org.apache.groovy:groovy:${project.groovy_version}"
    api "org.apache.groovy:groovy-contracts:${project.groovy_version}"
    api group: 'org.kohsuke', name: 'github-api', version: project.ghapi_version

    sampleCompileOnly(sourceSets.main.output)

    project(':modules').subprojects.each {
        sampleCompileOnly(project(path: it.path, configuration: 'configOut'))
        configSources(project(path: it.path, configuration: 'configOutSrc'))
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact(tasks.groovydocJar)
            artifactId = 'camelot-config'
            pom {
                name = 'Camelot configuration'
                description = 'Configuration for the Camelot Discord bot'
                rootProject.pomUtils.githubRepo(it, 'Camelot')
                rootProject.pomUtils.neoForgedDeveloper(it)
                rootProject.pomUtils.license(it, PomUtilsExtension.License.MIT)
            }
        }
    }
}

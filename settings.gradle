pluginManagement {
    repositories {
        gradlePluginPortal()
        maven { url = 'https://maven.neoforged.net/' }
    }
}

plugins {
    id('org.gradle.toolchains.foojay-resolver-convention') version '0.8.0'
}

dependencyResolutionManagement {
    repositories {
        mavenCentral()
        maven {
            name 'm2-dv8tion'
            url 'https://m2.dv8tion.net/releases'
        }
        maven {
            name 'jda-chewtils'
            url 'https://m2.chew.pro/releases'
        }
        maven {
            name 'jitpack'
            url 'https://jitpack.io'
        }
    }

    versionCatalogs {
        create('libs') {
            final jdbi = version('jdbi', '3.32.0')
            library('jdbi-core', 'org.jdbi', 'jdbi3-core').versionRef(jdbi)
            library('jdbi-sqlobject', 'org.jdbi', 'jdbi3-sqlobject').versionRef(jdbi)
            bundle('jdbi', ['jdbi-core', 'jdbi-sqlobject'])

            final jackson = version('jackson', '2.13.3')
            library('jackson-yaml', 'com.fasterxml.jackson.dataformat', 'jackson-dataformat-yaml').versionRef(jackson)
            bundle('jackson', ['jackson-yaml'])
        }
    }
}

rootProject.name = 'Camelot'

include ':config'

include ':modules:info-channels'
include ':modules:file-preview'
